' First run Test_ReadFile_Demo in MATLAB, then run this script file.

' ------------------------------------------------------------------------------
' Input definitions
const pathSample$ := "C:\\Spike7\\data\\Demo.smr";
const ch% := 1;
const fs% := 100;
const tSample := 1; 's
const tStart := 0;
const tEnd   := 120;

' Work directory
const pathBase$ := "C:\\MPC";
const pathFileY$ := pathBase$ + "\\" + "y.bin";
const pathFileU$ := pathBase$ + "\\" + "u.bin";
const pathLock$  := pathBase$ + "\\" + "lock";

' General state variables
const nData% := tSample*fs%;
var bufferY[nData%];
var bufferU[nData%];
var tCur;

var vSample%;
var vFileL%;
var vFileY%;
var vFileU%;


' ==============================================================================
' Helper functions

Func Init();
'FilePathSet(pathBase$, 0, 1);   ' set current dir, create dir
'vSample% := View();
vSample% := FileOpen(pathSample$, 0, 0); '.smr, visible
vFileL% := FileOpen(pathLock$,  9, 1);  'binary, write (new file)
vFileY% := FileOpen(pathFileY$, 9, 1);  'binary, write (new file)
vFileU% := FileOpen(pathFileU$, 9, 0);  'binary, read (existing file)
'PrintLog(vSample%);
'PrintLog(vFileY%);
'PrintLog(vFileU%);
end;


' ------------------------------------------------------------------------------
Func Exit();
' Close input/output files
View(vFileY%);
FileClose();

View(vFileU%);
FileClose();

' Close and delete lock file
View(vFileL%);
FileClose();
FileDelete(pathLock$);
end;


' ------------------------------------------------------------------------------
Func Update();
var ret%, pos%;

View(vSample%);
ret% := ChanData(ch%, bufferY, tCur, tCur+tSample);
'PrintLog("Sampled %d\n", ret%);

View(vFileY%);
ret% := BWriteSize(8, bufferY); '64-bit double
pos% := BSeek(0, 2); 'move to end of file (flush)
'PrintLog("Wrote %d (pos: %d)\n", ret%*nData%, pos%);

View(vFileU%);
repeat
    YieldSystem();
    ret% := BReadSize(8, bufferU); '64-bit double
until ret% > 0;
'PrintLog("Read %d\n", ret%);

end;


' ==============================================================================
' Run script

Init();
for tCur := tStart to tEnd step tSample do
    Update();
    'Yield(1e-3);
next;
Exit();
